#!/usr/bin/perl

use strict;
use warnings;

use Getopt::Long;
use Proc::Daemon;
use Proc::PID::File;
use Log::Log4perl qw(get_logger);
use Data::Dumper;
use Time::Local;
use HTTP::Tiny;
use HTTP::Daemon;
use HTTP::Status;
use HTTP::Response;
use URI::QueryParam;
use HTML::Entities;
use JSON::Any;
use threads;
use threads::shared;
use Thread::Semaphore;
use Device::BCM2835;
use Device::SerialPort;
use POSIX qw(uname);
use Storable;

my $foreground = 0;
my $debug      = 0;
GetOptions ('foreground' => \$foreground,
            'debug'      => \$debug);

my $conf = q(
    log4perl.logger.foreground         = TRACE, Screen
    log4perl.logger.daemon             = INFO, Logfile

    log4perl.appender.Logfile          = Log::Log4perl::Appender::File
    log4perl.appender.Logfile.filename = /var/log/flud.log
    log4perl.appender.Logfile.mode     = append
    log4perl.appender.Logfile.create_at_logtime = true
    log4perl.appender.Logfile.layout   = Log::Log4perl::Layout::PatternLayout
    log4perl.appender.Logfile.layout.ConversionPattern = %d %-5p - %m{chomp}%n

    log4perl.appender.Screen           = Log::Log4perl::Appender::Screen
    log4perl.appender.Screen.stderr    = 0
    log4perl.appender.Screen.layout    = Log::Log4perl::Layout::SimpleLayout
);

unless ($foreground) {
    die 'Must run as root to daemonize, or with --foreground' unless $< == 0;
    # Fork and detach from the parent process
    eval { Proc::Daemon::Init(); };
    die "Unable to start daemon: $@" if ($@);
    # Get a PID file
    die ('Already running as daemon, exiting this instance')
        if Proc::PID::File->running();
}

# Start log
Log::Log4perl::init (\$conf);
my $log = Log::Log4perl->get_logger ($foreground ? 'foreground' : 'daemon');
$log->info ('starting');

# Setup signal handlers so that we have time to cleanup before shutting down
my $keep_going = 1;
$SIG{INT}  = sub { $log->info ('caught SIGINT: exiting gracefully');  $keep_going = 0; };
$SIG{QUIT} = sub { $log->info ('caught SIGQUIT: exiting gracefully'); $keep_going = 0; };
$SIG{TERM} = sub { $log->info ('caught SIGTERM: exiting gracefully'); $keep_going = 0; };


my $SECS_PER_MIN  = 60;
my $SECS_PER_HOUR = 60 * 60;

my $WEATHER_UPDATE_INTERVAL  = (30 * $SECS_PER_MIN); # seconds
my $CALENDAR_UPDATE_INTERVAL = (15 * $SECS_PER_MIN); # seconds
my $STATUS_UPDATE_INTERVAL   =                    1; # seconds

# Connect the shift register pins to these GPIO pins.
my $shift_pin = &Device::BCM2835::RPI_GPIO_P1_07; # Clock D6 -> SH_CP 11 (shift clock)
my $store_pin = &Device::BCM2835::RPI_GPIO_P1_15; # Latch D7 -> ST_CP 12 (storage clock)
my $data_pin  = &Device::BCM2835::RPI_GPIO_P1_13; # Data  D5 -> DS    14 (data in)
my $noe_pin   = &Device::BCM2835::RPI_GPIO_P1_11; # ~OE   A1 -> ~OE   13 (not output enable)
# connect rPi 2 5v  to VCC 16
# connect rPi 6 GND to GND  8
# connect rPi 6 GND to ~OE 13 (output enable, active low) to ground


my $conf_file = '/etc/flud.conf';
my $config    = do $conf_file;
unless (defined $config) {
    $config = {google_feed         => undef,
               google_private      => undef,
               wunderground_apikey => undef,
               xively_feed         => undef,
               xively_apikey       => undef,
               zipcode             => undef,
               rain_threshold      => undef,
               moisture_threshold  => undef,
               valve_specs => [map { {name=>"valve $_", duration=>1} } 1..8]};
    open (my $CONF, ">", $conf_file);
    print $CONF Data::Dumper->Dump([$config], ['$config']);
    close $CONF;
}
$config = shared_clone ($config);


my $last_weather_check  = 0;
my $last_status_print   = 0;
my $last_calendar_check = 0;
my $last_time_sync      = 0;

my $semaphore = new Thread::Semaphore;
my $rainfall   :shared  = 0;
my $trigger    :shared  = 0;
my $dirtmon    :shared  = 1;  # set to 0 to stop watching dirtmon
my $moisture   :shared  = 0;
my $voltage    :shared  = 0.0;
my $valveOn    :shared  = 0;  # 1-based; 0 means "all off"

sub timestamp {
    my ($time) = @_;
    my ($sec,$min,$hour,$mday,$mon,$year) = gmtime($time);
    return sprintf ('%04d-%02d-%02dT%02d:%02d:%02dZ', $year+1900, $mon+1, $mday, $hour, $min, $sec);
}


sub printStatus {
  my $current = time;
  if ($valveOn) {
      $log->debug (sprintf ('v%d %02d:%02d',
                            $valveOn,
                            ($trigger - $current) / $SECS_PER_MIN,
                            ($trigger - $current) % $SECS_PER_MIN));
  }
  elsif ($trigger > $current) {
      $log->debug (sprintf ('%f in %02d:%02d', $rainfall,
                            ($trigger - $current) / $SECS_PER_MIN,
                            ($trigger - $current) % $SECS_PER_MIN));
  }
  else {
  #    $log->trace (sprintf ('%f in', $rainfall));
  }
  $last_status_print = time;
}


sub getXivelyURL {
  return 'http://api.xively.com/v2/feeds/' . $config->{xively_feed};
}


sub putXivelyData {
    return unless defined $config->{xively_feed} && defined $config->{xively_apikey};
    my ($stream, $value) = @_;

    $log->debug ('putting xively data');
    my $url = getXivelyURL();
    my $json = qq( { "version" : "1.0.0", "datastreams" : [ {"id" : "$stream", "current_value" : "$value"} ] } );
    my $response = HTTP::Tiny->new->request ('PUT', $url,
                                             {'headers' => {
                                                            'Host'         => 'api.xively.com',
                                                            'X-ApiKey'     => $config->{xively_apikey},
                                                            'Content-Type' => 'application/json; charset=UTF-8',
                                                            'Accept'       => 'application/json'},
                                              'content' => $json});
    if ($response->{success}) {
        $log->trace ("xively replied '$response->{status}'");
    } else {
        $log->error ("xively replied '$response->{status}'");
        $log->trace (Dumper($response));
    }
}


sub getWeatherURL {
  return 'http://api.wunderground.com/api/' . $config->{wunderground_apikey} .
    '/forecast/q/' .
    $config->{zipcode} . '.json';
}


sub checkWeather {
    return unless defined $config->{wunderground_apikey} && defined $config->{zipcode};
    $log->info ("retrieving weather for zipcode '$config->{zipcode}'");
    $rainfall = 0;
    my $url = getWeatherURL();
    $log->trace ($url);
    my $weather = HTTP::Tiny->new->get($url)->{content};
    $log->trace ($weather);
    my $data = JSON::Any->jsonToObj($weather);
    if ($data) {
      foreach (@{$data->{forecast}->{simpleforecast}->{forecastday}}) {
        $rainfall += $_->{qpf_allday}->{in};
      }
      $log->info ("rainfall = $rainfall in");
      putXivelyData ("forecast.rainfall", $rainfall);
    }
    else {
        $log->error ('error getting weather');
    }
    $last_weather_check = time;
}


sub getCalendarURL {
  return 'http://www.google.com/calendar/feeds/' .
    ${config}->{google_feed} . '@group.calendar.google.com/' .
    'private-' . ${config}->{google_private} . '/full' .
    "?fields=entry(gd:when,title[text()='flud'])" .
    '&singleevents=true&prettyprint=true&max-results=1&orderby=starttime&sortorder=a&' .
    '&start-min=' . timestamp(time) .
    '&start-max=' . timestamp(time + 2 * $CALENDAR_UPDATE_INTERVAL);
}


sub checkCalendar {
    return unless $config->{google_feed} && defined $config->{google_private};
    $log->info ('retrieving calendar');
    $semaphore->down;
    $trigger = 0;
    my $url = getCalendarURL();
    $log->trace ($url);
    my $event = HTTP::Tiny->new->get($url)->{content};
    $log->trace ($event);
    if ($event =~ /startTime='(\d{4})-(\d{2})-(\d{2})T(\d{2}):(\d{2}):(\d{2})\.\d{3}([\+\-])(\d{2}):(\d{2})'/) {
        my ($year, $mon, $mday, $hour, $min, $sec, $zone_sign, $zone_hour, $zone_min) =
                ($1, $2, $3, $4, $5, $6, $7, $8, $9);
        $trigger = timegm ($sec, $min, $hour, $mday, $mon-1, $year-1900) -
                (($zone_sign eq '-' ? -1 : 1) * ($zone_hour * $SECS_PER_HOUR + $zone_min * $SECS_PER_MIN));
    }
    $semaphore->up;
    $last_calendar_check = time;
}


sub initValves {
    # call set_debug(1) to do a non-destructive test on non-RPi hardware
    my ($sysname, $nodename, $release, $version, $machine) = POSIX::uname();
    Device::BCM2835::set_debug($machine ne 'armv6l' || $debug);
    Device::BCM2835::init() or $log->logdie ('could not init library');

    Device::BCM2835::gpio_fsel ($data_pin,
                              &Device::BCM2835::BCM2835_GPIO_FSEL_OUTP);
    Device::BCM2835::gpio_fsel ($shift_pin,
                              &Device::BCM2835::BCM2835_GPIO_FSEL_OUTP);
    Device::BCM2835::gpio_fsel ($store_pin,
                              &Device::BCM2835::BCM2835_GPIO_FSEL_OUTP);
    Device::BCM2835::gpio_fsel ($noe_pin,
                              &Device::BCM2835::BCM2835_GPIO_FSEL_OUTP);

    Device::BCM2835::gpio_write ($data_pin,  0);
    Device::BCM2835::gpio_write ($shift_pin, 0);
    Device::BCM2835::gpio_write ($store_pin, 0);
    Device::BCM2835::gpio_write ($noe_pin,   0);
}


sub commitValves {
    Device::BCM2835::gpio_write ($shift_pin, 0);
    Device::BCM2835::gpio_write ($store_pin, 0);
    for (my $ii = 8; $ii > 0; --$ii) {
        Device::BCM2835::gpio_write ($shift_pin, 0);
        Device::BCM2835::gpio_write ($data_pin, $valveOn == $ii ? 1 : 0);
        Device::BCM2835::gpio_write ($shift_pin, 1);
    }
    Device::BCM2835::gpio_write ($store_pin, 1);
    for (my $ii = 8; $ii > 0; --$ii) {
        putXivelyData ("valve." . $ii, $valveOn == $ii ? 1 : 0);
    }
}


sub stopValves {
    $log->info ('stopping valves');
    $semaphore->down;
    $valveOn = 0;
    $trigger = 0;
    $semaphore->up;
    commitValves();
}


sub advanceValves {
    $semaphore->down;

    do {
        ++$valveOn;
    } while ($valveOn <= (scalar @{$config->{valve_specs}}) && !$config->{valve_specs}->[$valveOn-1]->{duration});

    if ($valveOn > scalar @{$config->{valve_specs}}) {
        $log->info ('valve schedule complete');
        $valveOn = 0;
        $trigger = 0;
    }
    else {
        $log->info (sprintf (qq(advancing to run valve #%d - "%s" for %d minutes),
                             $valveOn,
                             $config->{valve_specs}->[$valveOn-1]->{name},
                             $config->{valve_specs}->[$valveOn-1]->{duration}));
        $trigger = time + $config->{valve_specs}->[$valveOn-1]->{duration} * $SECS_PER_MIN;
    }
    $semaphore->up;

    commitValves();
}


sub startValve {
  my ($valve) = @_;

  $semaphore->down;
  $valveOn = $valve;
  $log->info (sprintf (qq(starting valve #%d - "%s" for %d minutes),
                       $valveOn,
                       $config->{valve_specs}->[$valveOn-1]->{name},
                       $config->{valve_specs}->[$valveOn-1]->{duration}));
  $trigger = time + $config->{valve_specs}->[$valveOn-1]->{duration} * $SECS_PER_MIN;
  $semaphore->up;

  commitValves();
}


sub dirtmon {
  my $PORT = '/dev/ttyUSB0';

  my $ob = Device::SerialPort->new ($PORT) || $log->logdie ("Cannot create device $PORT: $!");
  $ob->baudrate(57600)   || $log->logdie ("failed setting baudrate");
  $ob->parity("none")    || $log->logdie ("failed setting parity");
  $ob->databits(8)       || $log->logdie ("failed setting databits");
  $ob->handshake("none") || $log->logdie ("failed setting handshake");
  $ob->write_settings    || $log->logdie ("no settings");
  open (my $DEV, "+<", $PORT)  || $log->logdie ("Cannot open $PORT: $_");

  print $DEV '1i 212g\n'; # node 1 in group 212

  while ($dirtmon) {
    while ($_ = <$DEV>) {
      # byte -> 0  1  2  3  4  5  6   7   8  9  10  11  12
      #         ====  ----------  - --- ---  -----  ------
      # eg   -> OK 2  2  0  0  0  2 115 117  0   0   0   0
      #  long ping;      // 32-bit counter
      #  byte id :7;     // identity, should be different for each node
      #  byte boost :1;  // whether compiled for boost chip or not
      #  byte vcc1;      // VCC before transmit, 1.0V = 0 .. 6.0V = 250
      #  byte vcc2;      // battery voltage (BOOST=1), or VCC after transmit (BOOST=0)
      #  word sensor;    // sensor1
      #  word sensor;    // sensor2
      if (/^OK 2/) {
        my @rec = split(' ', $_);
        my $ping  = $rec[2] + $rec[3] * 256 + $rec[4] * 256 * 256 + $rec[5] * 256 * 256 * 256;
        my $id    = $rec[6];
        my $vcc1  = $rec[7] / 250.0 * 5.0 + 1.0;
        my $vcc2  = $rec[8] / 250.0 * 5.0 + 1.0;
        my $sensor1 = $rec[9] + $rec[10] * 256;
        my $sensor2 = $rec[11] + $rec[12] * 256;
        $log->info (join (' ', 'dirtmon', $ping, $id, $vcc1, $vcc2, $sensor1, $sensor2));
        putXivelyData ("dirtmon1.vcc1", $vcc1);
        putXivelyData ("dirtmon1.vcc2", $vcc2);
        putXivelyData ("dirtmon1.moisture1", $sensor1);
        putXivelyData ("dirtmon1.moisture2", $sensor2);
        $moisture = ((1023 - $sensor1) + (1023 - $sensor2)) / 2;
        $voltage  = ($vcc1 + $vcc2) / 2.0;
      }
    }
    sleep 1;
  }
  close $DEV;
}


sub run_webserver {
    my $sock = IO::Socket::INET->new (PeerAddr => 'example.com',
                                      PeerPort => 80,
                                      Proto    => 'tcp');
    my $localip = $sock->sockhost;
    my $d = HTTP::Daemon->new (ReuseAddr => 1,
                               LocalAddr => $localip,
                               LocalPort => 8888,
                               Listen    => 20) || die;

    $log->info ('web server started at ' . $d->sockhost() . ':' .$d->sockport());

    while (my $c = $d->accept) {
        threads->create (\&process_one_req, $c)->detach();
    }
}


sub send_index {
    my ($client) = @_;
    my $response = HTTP::Response->new(200);
    $response->header ('Content-Type' => 'text/html');

    my $body = join (' ',
        '<script src="http://ajax.googleapis.com/ajax/libs/jquery/1.10.2/jquery.min.js"></script>',
        '<script>',
        'function indicateValve (num) {
          $("div.running").hide ();
          $("div.running").filter (function (index) {return index + 1 == num;}).show ("fast");
         }',
        'function startValve (num) {
          $.ajax({
             url: "/api/startValve",
             data: {
               valve: num + 1
             },
             success: function (data) {
                $("#status").html ("<strong>success " + data + "</strong>");
                indicateValve (data);
             }
           });
         }',
        'function stop () {
          $.ajax({
             url: "/api/stop",
             success: function (data) {
                $("#status").html ("<strong>success " + data + "</strong>");
                indicateValve (data);
             }
           });
         }',
        'function advance () {
          $.ajax({
             url: "/api/advance",
             success: function (data) {
                $("#status").html ("<strong>success " + data + "</strong>");
                indicateValve (data);
             }
           });
         }',
        'function refreshStatus () {
          $.ajax({
             url: "/api/getStatus",
             dataType: "json",
             success: function (data) {
                $("#timestamp").html(data.timestamp);
                $("#trigger").html(data.trigger);
                $("#rainfall").html(data.rainfall);
                indicateValve (data.valveOn);
             }
           });
         }',
        '$(document).ready(function () {
           refreshStatus ();
           setInterval (refreshStatus, 30 * 1000);
         });',
        '</script>',
        '<div id="status"></div>',
        'Server timestamp =    <div style="display:inline" id="timestamp">', time,      '</div><br>',
        'Next trigger =        <div style="display:inline" id="trigger">',   $trigger,  '</div><br>',
        'Forecasted rainfall = <div style="display:inline" id="rainfall">',  $rainfall, '</div> in<br>',
        'Soil moisture =       <div style="display:inline" id="moisture">',  $moisture, '</div><br>',
        'Sensor voltage =      <div style="display:inline" id="voltage">',   $voltage,  '</div>V<br>',
        '<br>',
        "Valves <form method='POST'>"
    );
    for (my $ii = 0; $ii < scalar @{$config->{valve_specs}}; ++$ii) {
        $body .= "<input type='text' name='n$ii' value='" . encode_entities($config->{valve_specs}->[$ii]->{name}) . "' />";
        $body .= "<input type='text' name='d$ii' value='$config->{valve_specs}->[$ii]->{duration}' />";
        $body .= "<input type='button' name='s$ii' value='start' onclick='startValve($ii)' />";
        $body .= "<div class='running' style='display:inline;'>\&lt;-- ON</div>";
        $body .= "<br>";
    }
    $body .= "<input type='button' value='advance' onclick='advance()' />";
    $body .= "<input type='button' value='stop' onclick='stop()' />";
    $body .= "<br><br>";
    $body .= "Google calendar XML <input type='text' name='gcal' value='" .
      encode_entities("http://www.google.com/calendar/feeds/$config->{google_feed}\@group.calendar.google.com/private-$config->{google_private}/full") . "' /><br>";
    $body .= "<a href='" . getCalendarURL() . "'>View Calendar</a><br>";
    $body .= "Wunderground API key<input type='text' name='wunderground_apikey' value='$config->{wunderground_apikey}' /><br>";
    $body .= "Zipcode <input type='text' name='zipcode' value='$config->{zipcode}' /><br>";
    $body .= "<a href='" . getWeatherURL() . "'>View Weather</a><br>";
    $body .= "Xively API key<input type='text' name='xively_apikey' value='$config->{xively_apikey}' /><br>";
    $body .= "Xively feed id<input type='text' name='xively_feed' value='$config->{xively_feed}' /><br>";
    $body .= "<a href='" . getXivelyURL() . "'>View Xively</a><br>";
    $body .= "Rain threshold<input type='text' name='rain_threshold' value='$config->{rain_threshold}' /><br>";
    $body .= "Moisture threshold<input type='text' name='moisture_threshold' value='$config->{moisture_threshold}' /><br>";
    $body .= "<input type='hidden' name='h' />";
    $body .= "<input type='submit' value='Submit' /></form>";

    $response->content($body);
    $client->send_response($response);
}


sub process_one_req {
    STDOUT->autoflush(1);
    my $client = shift;
    while (my $r = $client->get_request) {
        $log->trace (Data::Dumper->Dump([$r], ['$r']));
        if ($r->method eq "GET") {
            if ($r->uri->path eq "/api/advance") {
                advanceValves ();
                my $response = HTTP::Response->new(200);
                $response->header ('Content-Type' => 'text/html');
                $response->content ($valveOn);
                $client->send_response ($response);
            } elsif ($r->uri->path eq "/api/stop") {
                stopValves ();
                my $response = HTTP::Response->new(200);
                $response->header ('Content-Type' => 'text/html');
                $response->content ($valveOn);
                $client->send_response ($response);
            } elsif ($r->uri->path =~ "/api/startValve") {
                startValve ($r->uri->query_param('valve'));
                my $response = HTTP::Response->new(200);
                $response->header ('Content-Type' => 'text/html');
                $response->content ($valveOn);
                $client->send_response ($response);
            } elsif ($r->uri->path =~ "/api/getStatus") {
                my $response = HTTP::Response->new(200);
                $response->header ('Content-Type' => 'text/json');
                my $now = time;
                my $content = qq({"timestamp" : $now, "trigger" : $trigger, "rainfall" : $rainfall, "valveOn" : $valveOn});
                $response->content ($content);
                $client->send_response ($response);
              } else {
                send_index ($client);
            }
        }
        elsif ($r->method eq "POST") {
            my $content = $r->content;
            while ($content =~ s/n(\d+)=(.*?)&//) {
                my ($index, $name) = ($1, $2);
                $name =~ s/\+/ /g;
                $name =~ s/%([0-9A-Fa-f]{2})/chr(hex($1))/eg;
                $config->{valve_specs}->[$index]->{name} = $name;
            }
            while ($content =~ s/d(\d+)=(\d+)&//) {
                $config->{valve_specs}->[$1]->{duration} = int ($2);
            }
            while ($content =~ s{gcal=(.*?)&}{}) {
                my $value = $1;
                $value =~ s/\+/ /g;
                $value =~ s/%([0-9A-Fa-f]{2})/chr(hex($1))/eg;
                $value =~ m{https?://www\.google\.com/calendar/feeds/(.*)(?:(?:\@)|(?:\%40))group\.calendar\.google\.com/private-(.*?)/.*};
                ($config->{google_feed}, $config->{google_private}) = ($1, $2);
            }
            foreach my $name (qw(wunderground_apikey zipcode xively_apikey xively_feed rain_threshold moisture_threshold)) {
                while ($content =~ s/${name}=([-\d.A-Za-z]+)&//) {
                    my $value = $1;
                    $value =~ s/\+/ /g;
                    $value =~ s/%([0-9A-Fa-f]{2})/chr(hex($1))/eg;
                    $config->{$name} = $value;
                }
            }
            open (my $CONF, ">", $conf_file);
            print $CONF Data::Dumper->Dump([$config], ['$config']);
            close $CONF;
            send_index ($client);
        }
        else {
            $client->send_error (RC_FORBIDDEN);
        }
    }
    $client->close;
    undef ($client);
}


initValves();
stopValves();

threads->create (\&run_webserver);
threads->create (\&dirtmon);

while ($keep_going) {
    if (0) {}
    elsif (!$valveOn && (time - $last_weather_check >= $WEATHER_UPDATE_INTERVAL)) {
        checkWeather();
    }
    elsif (!$valveOn && (time - $last_calendar_check >= $CALENDAR_UPDATE_INTERVAL)) {
        checkCalendar();
    }
    elsif ($trigger && $trigger < time) {
        if ($valveOn || ($rainfall <= $config->{rain_threshold} && $moisture <= $config->{moisture_threshold})) {
            advanceValves();
        }
        else {
            $log->info ("rain delay");
            $trigger += $WEATHER_UPDATE_INTERVAL;
        }
    }
    elsif (time - $last_status_print >= $STATUS_UPDATE_INTERVAL) {
        printStatus();
    }
    sleep (1);
}

stopValves();

# Mark a clean exit in the log
$log->info ('exiting');

__END__
